plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'maven-publish'
    id 'io.github.juuxel.loom-quiltflower' version '1.7.1'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "${project.archives_base_name}-${project.minecraft_version}"
version = project.mod_version
group = project.maven_group

repositories {
    mavenLocal()
    maven { // WhereIsIt
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/jackfred2/WhereIsIt")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
            password = project.findProperty("gpr.key") ?: System.getenv("GPR_TOKEN")
        }
        content {
            includeGroup("red.jackf")
        }
    }
    maven { url = "https://maven.fabricmc.net" } // Fabric
    maven { url = "https://maven.shedaniel.me/" } // REI & Cloth Config
    maven { url = 'https://maven.terraformersmc.com/' } // ModMenu

    maven { url = "https://server.bbkr.space/artifactory/libs-release" } // UniversalComponents, LibGui
    maven { // Curse, Expanded Storage
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    /*maven { // CardinalComponents (UniversalComponents lib)
        name = "Ladysnake Libs"
        url = "https://dl.bintray.com/ladysnake/libs"
    }*/
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}")
    //modImplementation("dev.architectury:architectury-fabric:${project.architectury_version}")
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
//    modImplementation "me.shedaniel.cloth.api:cloth-client-events-v0:${project.cloth_client_version}"
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"

    modImplementation include("red.jackf:WhereIsIt:${project.whereisit_version}")
    modImplementation include("io.github.cottonmc:LibGui:${project.libgui_version}")
    implementation 'org.jetbrains:annotations:16.0.2'

    // Compat

    //modCompileOnly "io.github.cottonmc:UniversalComponents:${project.universal_components_version}"
    //modCompileOnly "curse.maven:expanded-storage-317856:${project.expanded_storage_file_id}"
    //modCompileOnly "curse.maven:ellemes-container-library-530668:${project.ellemes_container_library_file_id}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

curseforge {
    if (project.hasProperty("cf.key")) {
        apiKey = project.findProperty("cf.key")
        project {
            id = "397217"
            changelog = "Check the GitHub for changes: https://github.com/JackFred2/ChestTracker"
            releaseType = "release"

            addGameVersion "Fabric"
            addGameVersion "Java 17"

            project.game_versions.tokenize(",").each { addGameVersion it }

            mainArtifact(remapJar) {
                relations {
                    requiredDependency("fabric-api")
                    requiredDependency("cloth-config")
                    optionalDependency("modmenu")
                    optionalDependency("roughly-enough-items")
                    embeddedLibrary("libgui")
                    embeddedLibrary("where-is-it")
                }
                displayName = "${project.prefix} ${archivesBaseName}-${version}.jar"
            }

        }

        options {
            forgeGradleIntegration = false
        }
    }
}

modrinth {token = project.findProperty("modrinth.key")
    projectId = 'ni4SrKmq'
    versionNumber = project.mod_version
    versionName = "ChestTracker $project.mod_version"
    versionType = "release"
    uploadFile = remapJar
    changelog = "Check the GitHub for changes: https://github.com/JackFred2/ChestTracker"
    gameVersions = project.game_versions.tokenize(",")
    loaders = ["fabric", "quilt"]
    dependencies {
        required.project("9s6osm5g") // cloth config
        required.project("P7dR8mSH") // fabric api
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        mavenLocal()
    }
}
